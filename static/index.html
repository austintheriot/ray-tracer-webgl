<!doctype html>
<html lang="en">

<head>
    <meta charset="utf-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>Ray Tracer WebGL</title>
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Roboto:wght@300;400;500;700&display=swap" rel="stylesheet">
    <script src="/main.js" defer></script>
    <style>
        canvas {
            width: 100%;
            max-width: 100vw;
            image-rendering: pixelated;
            object-fit: contain;
        }
    </style>
</head>

<body>
    <canvas></canvas>
    <button>Save Image</button>
    <script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
        in vec4 a_position;

        out vec2 v_position;

        void main() {
            v_position = a_position.xy;
            gl_Position = a_position;
        }
      </script>

    <script id="fragment-shader" type="x-shader/x-fragment">#version 300 es
        precision highp float;

        uniform float u_width;
        uniform float u_height;

        struct Ray {
            vec3 origin;
            vec3 direction;
        };

        struct Sphere {
            vec3 center;
            float radius;
        };

        struct HitRecord {
            vec3 hit_point;
            float hit_t;
            vec3 normal;
            bool front_face;
        };

        out vec4 o_color;

        in vec2 v_position;

        vec3 ray_at(in Ray r, float hit_t) {
            return r.origin + r.direction * hit_t;
        }

        float length_squared(in vec3 v) {
            return pow(length(v), 2.);
        }

        void set_hit_record_front_face(inout HitRecord hit_record, in Ray r, in vec3 outward_normal) {
            hit_record.front_face = dot(r.direction, outward_normal) < 0.;
            if (hit_record.front_face) {
                hit_record.normal = outward_normal;
            } else {
                hit_record.normal = -outward_normal;
            }
        }

        bool hit_sphere(in Sphere sphere, in Ray r, in float t_min, in float t_max, inout HitRecord hit_record) {
            vec3 oc = r.origin - sphere.center;
            float a = length_squared(r.direction);
            float half_b = dot(oc, r.direction);
            float c = length_squared(oc) - pow(sphere.radius, 2.);
            float discriminant = pow(half_b, 2.) - a * c;

            // no hit
            if (discriminant < 0.) return false;

            // there was a hit, but it's not within an acceptable range
            float sqrtd = sqrt(discriminant);
            float root = (-half_b - sqrtd) / a;
            if (root < t_min || t_max < root) {
                root = (-half_b + sqrtd) / a;
                if (root < t_min || t_max < root) {
                    return false;
                }
            }

            // hit is within acceptable range: calculate hit point, normal, and t
            hit_record.hit_t = root;
            hit_record.hit_point = ray_at(r, hit_record.hit_t);
            vec3 outward_normal = (hit_record.hit_point - sphere.center) / sphere.radius;
            set_hit_record_front_face(hit_record, r, outward_normal);

           return true;
        }

        vec3 ray_color(in Ray r) {
            Sphere sphere_1 = Sphere(vec3(0., 0., -1.), 0.5);

            // hit record gets modified with hit details if there was a hit
            HitRecord hit_record = HitRecord(vec3(0.), 0., vec3(0.), false);
            bool was_hit = hit_sphere(sphere_1, r, 0.001, 100000., hit_record);
            if (was_hit) {
                return (hit_record.normal + 1.) * 0.5;
            }

            vec3 unit_direction = normalize(r.direction);
            float t = 0.5 * (unit_direction.y + 1.0);
            return mix(vec3(1.0, 1.0, 1.0), vec3(0.5, 0.7, 1.0), t);
        }
        
        void main() {
            // these could be initialized as uniforms
            float aspect_ratio = u_width / u_height;
            float viewport_height = 2.0;
            float viewport_width = aspect_ratio * viewport_height;
            float focal_length = 1.0;
            vec3 camera_origin = vec3(0.);
            vec3 viewport_horizontal_vec = vec3(viewport_width, 0., 0.);
            vec3 viewport_vertical_vec = vec3(0., viewport_height, 0.);
            vec3 lower_left_corner = camera_origin - viewport_horizontal_vec / 2. - viewport_vertical_vec / 2. - vec3(0., 0., focal_length);

            // map from -1->1 to 0->1
            vec2 uv = (v_position + 1.) * 0.5;
            
            // create ray from camera origin to viewport
            vec3 ray_direction = lower_left_corner + uv.x * viewport_horizontal_vec + uv.y * viewport_vertical_vec - camera_origin;
            Ray r = Ray(camera_origin, ray_direction);
            
            // determine color by shooting ray
            vec3 color = ray_color(r);

            o_color = vec4(color, 1.);
          }
      </script>
</body>

</html>