<!doctype html>
<html lang="en">

<head>
    <meta charset="utf-8" />
    <meta name="viewport" content="width=device-width, initial-scale=1" />
    <title>Ray Tracer WebGL</title>
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Roboto:wght@300;400;500;700&display=swap" rel="stylesheet">
    <script src="/main.js" defer></script>
    <style>
        canvas {
            width: 100%;
            max-width: 100vw;
            object-fit: contain;
        }
    </style>
</head>

<body>
    <canvas></canvas>
    <button>Save Image</button>
    <script id="vertex-shader" type="x-shader/x-vertex">#version 300 es
        in vec4 a_position;

        out vec2 v_position;

        void main() {
            v_position = a_position.xy;
            gl_Position = a_position;
        }
      </script>

    <script id="fragment-shader" type="x-shader/x-fragment">#version 300 es
        precision highp float;

        // PSEUDO-RANDOM NUMBER GENERATORS //////////////////////////////////////////////////////
        // Hash functions by Nimitz:
        // https://www.shadertoy.com/view/Xt3cDn

        uint base_hash(uvec2 p) {
            p = 1103515245U*((p >> 1U)^(p.yx));
            uint h32 = 1103515245U*((p.x)^(p.y>>3U));
            return h32^(h32 >> 16);
        }

        vec2 hash2(inout float seed) {
            uint n = base_hash(floatBitsToUint(vec2(seed+=.1,seed+=.1)));
            uvec2 rz = uvec2(n, n*48271U);
            return vec2(rz.xy & uvec2(0x7fffffffU))/float(0x7fffffff);
        }

        vec3 hash3(inout float seed) {
            uint n = base_hash(floatBitsToUint(vec2(seed+=.1,seed+=.1)));
            uvec3 rz = uvec3(n, n*16807U, n*48271U);
            return vec3(rz & uvec3(0x7fffffffU))/float(0x7fffffff);
        }



        // INPUTS / OUTPUTS //////////////////////////////////////////////////////
        uniform float u_width;
        uniform float u_height;
        uniform float u_time;
        
        out vec4 o_color;
        
        in vec2 v_position;

        // STRUCTS //////////////////////////////////////////////////////
        struct Ray {
            vec3 origin;
            vec3 direction;
        };

        struct Sphere {
            vec3 center;
            float radius;
        };

        struct HitRecord {
            vec3 hit_point;
            float hit_t;
            vec3 normal;
            bool front_face;
        };

        // GLOBALS ////////////////////////////////////////////////////////
        const float PI = 3.141592653589793;
        const int MAX_DEPTH = 5;
        // max possible float
        const float MAX_T = 1e5; 
        const float MIN_T = 0.001;

        float global_seed = 0.;

        
        // FUNCTIONS //////////////////////////////////////////////////////
        vec3 ray_at(in Ray r, float hit_t) {
            return r.origin + r.direction * hit_t;
        }

        float length_squared(in vec3 v) {
            return pow(length(v), 2.);
        }

        vec3 random_in_unit_sphere() {
            vec3 random = (hash3(global_seed) * 2.) - 1.;
            float theta = random.x * 2.0 * PI;
            float phi = acos(2.0 * random.y - 1.0);
            float r = pow(random.z, 1. / 3.);
            float sin_theta = sin(theta);
            float cos_theta = cos(theta);
            float sin_phi = sin(phi);
            float cos_phi = cos(phi);
            float x = r * sin_phi * cos_theta;
            float y = r * sin_phi * sin_theta;
            float z = r * cos_phi;
            return vec3(x, y, z);
        }

        vec3 random_unit_vec() {
           return normalize(random_in_unit_sphere());
        }

        // records whether a hit happened to the front or back face of an object
        void set_hit_record_front_face(inout HitRecord hit_record, in Ray r, in vec3 outward_normal) {
            hit_record.front_face = dot(r.direction, outward_normal) < 0.;
            if (hit_record.front_face) {
                hit_record.normal = outward_normal;
            } else {
                hit_record.normal = -outward_normal;
            }
        }

        bool hit_sphere(in Sphere sphere, in Ray r, in float t_min, in float t_max, inout HitRecord hit_record) {
            vec3 oc = r.origin - sphere.center;
            float a = length_squared(r.direction);
            float half_b = dot(oc, r.direction);
            float c = length_squared(oc) - pow(sphere.radius, 2.);
            float discriminant = pow(half_b, 2.) - a * c;

            // no hit
            if (discriminant < 0.) return false;

            // there was a hit, but it's not within an acceptable range
            float sqrtd = sqrt(discriminant);
            float root = (-half_b - sqrtd) / a;
            if (root < t_min || t_max < root) {
                root = (-half_b + sqrtd) / a;
                if (root < t_min || t_max < root) {
                    return false;
                }
            }

            hit_record.hit_t = root;
            hit_record.hit_point = ray_at(r, hit_record.hit_t);
            vec3 outward_normal = (hit_record.hit_point - sphere.center) / sphere.radius;
            set_hit_record_front_face(hit_record, r, outward_normal);
            return true;
        }

        bool hit_world(in Ray r, in float t_min, in float t_max, inout HitRecord hit_record) {
            // make list of hittable geometry
            Sphere sphere_list[] = Sphere[3](
                Sphere(vec3(0., 0., -1.), 0.5),
                Sphere(vec3(-0.5, 0., -0.5), 0.1),
                Sphere(vec3(0., -100.5, -1.), 100.)
            );
            
            // test whether any geometry was hit. If it was, the hit_record will be updated with
            // the new hit data if the new hit was closer to the camera than the previous hit
            bool hit_anything = false;
            float closest_so_far = t_max;
            HitRecord temp_hit_record;

            for (int i = 0; i < sphere_list.length(); i++) {
                Sphere sphere = sphere_list[i];
                if (hit_sphere(sphere, r, t_min, closest_so_far, temp_hit_record)) {
                    hit_anything = true;
                    closest_so_far = temp_hit_record.hit_t;
                    hit_record = temp_hit_record;
                }
            }

            return hit_anything;
        }

        // determine the color that a ray should be
        vec3 ray_color(in Ray r) {
             vec3 color = vec3(1.);

            for (int i = 0; i < MAX_DEPTH; i++) {
                // test for collisions with any geometry
                // hit record gets modified with hit details if there was a hit
                HitRecord hit_record = HitRecord(vec3(0., 0., -MAX_T), 0., vec3(0.), false);
                if (hit_world(r, MIN_T, MAX_T, hit_record)) {
                    vec3 random_direction = random_unit_vec();
                    vec3 target = hit_record.hit_point + hit_record.normal + random_direction;

                    // color diminishes on reflection
                    color *= 0.5;

                    // shoot ray off in random direction again
                    r.origin = hit_record.hit_point;
                    r.direction = target - r.origin;
                } else {
                    // no hit, return the sky gradient background
                    vec3 unit_direction = normalize(r.direction);
                    float t = 0.5 * (unit_direction.y + 1.0);
                    vec3 gradient = mix(vec3(1.0, 1.0, 1.0), vec3(0.5, 0.7, 1.0), t);
                    return color * gradient;
                }
            }

            return color;
        }
        
        void main() {
            // @todo: these can be initialized as uniforms instead
            int samples_per_pixel = 100;
            float aspect_ratio = u_width / u_height;
            float viewport_height = 2.0;
            float focal_length = 1.0;
            vec3 camera_origin = vec3(0.);
            float viewport_width = aspect_ratio * viewport_height;
            vec3 viewport_horizontal_vec = vec3(viewport_width, 0., 0.);
            vec3 viewport_vertical_vec = vec3(0., viewport_height, 0.);
            vec3 lower_left_corner = camera_origin - viewport_horizontal_vec / 2. - viewport_vertical_vec / 2. - vec3(0., 0., focal_length);
            
            // this seed initialization is by reinder https://www.shadertoy.com/view/llVcDz
            global_seed = float(base_hash(floatBitsToUint(v_position))) / float(0xffffffffU) + u_time;

            // get current position on viewport, mapped from -1->1 to 0->1
            // (i.e. percentage of width, percentage of height)
            vec2 uv = (v_position + 1.) * 0.5;

            // accumulate color per pixel
            vec3 color = vec3(0.);
            for (int i = 0; i < samples_per_pixel; i++) {
                vec2 random = hash2(global_seed);
                vec2 random_from_0_to_1 = (random * 0.5) + 1.0;
                vec2 random_within_pixel = random_from_0_to_1 / vec2(u_width, u_height);

                // uv +/- the value of 1 pixel
                vec2 randomized_uv = uv + random_within_pixel;

                // create ray from camera origin to viewport
                vec3 ray_direction = lower_left_corner + randomized_uv.x * viewport_horizontal_vec + randomized_uv.y * viewport_vertical_vec - camera_origin;
                Ray r = Ray(camera_origin, ray_direction);


                color += ray_color(r);
            }

            // scale color by number of samples
            float scale = (1. / float(samples_per_pixel));
            color *= scale;

            // gamma correction
            color = sqrt(color);
        
            o_color = vec4(color, 1.);
          }
      </script>
</body>

</html>